module analysis
imports
  libstratego-lib
  libstratego-sglr
  libstratego-sdf
  include/SugarTest
  lib/editor-common.generated

strategies

  record-all-sdfs =
    record-all(record-sdf) 

  record-all(s) =
    <readdir> ".";
    list-loop(
      if <isdir> <filemode> then
        chdir;
        record-all(s);
        <chdir> ".."
      else
        try(s)
      end
    )

  record-sdf:
    filename -> filename
    where 
      <has-extension(|"sdf")> filename;
      <open-import(abspath, parse-sdf-module-file, topdown(try(record-constructs)))> filename

  record-sdf:
    filename -> filename
    where 
      <has-extension(|"def")> filename; 
      <open-import(abspath, parse-sdf-definition-file, topdown(try(record-constructs)))> filename

  record-constructs = record-start-symbol + record-prod + record-cons

  record-prod:
    prod @ prod(lhs, sort, attrs) -> <id>
    where
      !sort;
      try(?cf(<id>) + ?lex(<id>));
      repeat(?label(_, <id>));
      (?sort(x) + ?parameterized-sort(x, _))
    with
      rules(
        SortDeclaration   :  x -> x
        SortProductionLHS :+ x -> lhs
      )
  record-cons: 
    term(cons(x)) -> <id>
    with 
      rules(
        Constructor : x -> x  
      )
      
  record-start-symbol:
    context-free-start-symbols(ss) -> <id>
    where 
      <map({s: ?sort(s); rules( TopSort : s -> s)})>ss
      
       
        